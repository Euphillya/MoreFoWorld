plugins {
    id 'java'
    id("io.papermc.paperweight.userdev") version "1.5.5"
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'me.hsgamer'
version = '1.0-SNAPSHOT'
description = 'A world registry for Folia'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    paperweight.foliaDevBundle("1.19.4-R0.1-SNAPSHOT")

    def core = "4.3.7"
    implementation "me.hsgamer:hscore-bukkit-baseplugin:$core"
    implementation "me.hsgamer:hscore-bukkit-config:$core"
    implementation "me.hsgamer:hscore-bukkit-utils:$core"
    implementation "me.hsgamer:hscore-config-proxy:$core"
    implementation "me.hsgamer:hscore-common:$core"
}

assemble {
    dependsOn shadowJar
    dependsOn reobfJar
}

shadowJar {
    relocate("me.hsgamer.hscore", "me.hsgamer.morefoworld.core")
    minimize()
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [
            version: version,
            name: project.name,
            apiVersion: "1.19",
            description: description
    ]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}
